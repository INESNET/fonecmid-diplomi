
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	СформироватьДвижения();
	
	РассчитатьПервичныеНачисления();
	
КонецПроцедуры

Процедура СформироватьДвижения()
	
	Для Каждого Строка Из ОсновныеНачисления Цикл
		
		Если ТипЗнч(Строка.ВидРасчета) = Тип("ПланВидовРасчетаСсылка.ВКМ_ОсновныеНачисления") Тогда
			Движение = Движения.ОсновныеНачисления.Добавить();
			Движение.ПериодРегистрации = Дата;
			Движение.ПериодДействияНачало = Строка.ДатаНачала;
			Движение.ПериодДействияКонец = Строка.ДатаОкончания;
			Движение.ВидРасчета = Строка.ВидРасчета;
			Движение.Специалист = Строка.Специалист;
			Движение.ГрафикРаботы = Строка.ГрафикРаботы;
		КонецЕсли;
		
		Если ТипЗнч(Строка.ВидРасчета) = Тип("ПланВидовРасчетаСсылка.ВКМ_ДополнительныеНачисления") Тогда
			Движение = Движения.ДополнительныеНачисления.Добавить();
			Движение.ПериодРегистрации = Дата;
			Движение.ВидРасчета = Строка.ВидРасчета;
			Движение.Специалист = Строка.Специалист;
			Движение.БазовыйПериодНачало = Строка.ДатаНачала;
			Движение.БазовыйПериодКонец = Строка.ДатаОкончания;
		КонецЕсли;
		
	КонецЦикла;
	
	Движения.ОсновныеНачисления.Записать();
	Движения.ДополнительныеНачисления.Записать();
	
КонецПроцедуры

Процедура РассчитатьПервичныеНачисления()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ОсновныеНачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки,
	               |	ОсновныеНачисленияДанныеГрафика.РабочихДнейФактическийПериодДействия КАК Факт,
	               |	ОсновныеНачисленияДанныеГрафика.РабочихДнейПериодДействия КАК План,
	               |	ЕСТЬNULL(СведенияОСотрудникахСрезПоследних.Оклад, 0) КАК Оклад
	               |ИЗ
	               |	РегистрРасчета.ОсновныеНачисления.ДанныеГрафика(
	               |			Регистратор = &Ссылка
	               |				И ВидРасчета.СпособРасчета = ЗНАЧЕНИЕ(Перечисление.СпособыРасчета.ОплатаПоОкладу)) КАК ОсновныеНачисленияДанныеГрафика
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОСотрудниках.СрезПоследних(
	               |				&Период,
	               |				(Подразделение, Сотрудник) В
	               |					(ВЫБРАТЬ
	               |						ОсновныеНачисления.Подразделение КАК Подразделение,
	               |						ОсновныеНачисления.ФизическоеЛицо КАК ФизическоеЛицо
	               |					ИЗ
	               |						РегистрРасчета.ОсновныеНачисления КАК ОсновныеНачисления
	               |					ГДЕ
	               |						ОсновныеНачисления.Регистратор = &Ссылка
	               |						И ОсновныеНачисления.ВидРасчета.СпособРасчета = ЗНАЧЕНИЕ(Перечисление.СпособыРасчета.ОплатаПоОкладу))) КАК СведенияОСотрудникахСрезПоследних
	               |		ПО ОсновныеНачисленияДанныеГрафика.Подразделение = СведенияОСотрудникахСрезПоследних.Подразделение
	               |			И ОсновныеНачисленияДанныеГрафика.ФизическоеЛицо = СведенияОСотрудникахСрезПоследних.Сотрудник";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Период", Дата);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Движение = Движения.ОсновныеНачисления[Выборка.НомерСтроки - 1];
		
		Если Выборка.План = 0 Тогда
			Движение.Результат = 0;
		Иначе
			Движение.Результат = Выборка.Оклад * Выборка.Факт / Выборка.План;
		КонецЕсли;
		
	КонецЦикла;
	
	Движения.ОсновныеНачисления.Записать(, Истина);
	
КонецПроцедуры

Процедура РассчитатьДополнительныеНачисления()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ДополнительныеНачисленияБазаОсновныеНачисления.НомерСтроки КАК НомерСтроки,
	               |	ДополнительныеНачисленияБазаОсновныеНачисления.РезультатБаза КАК РезультатБаза,
	               |	(РАЗНОСТЬДАТ(ДополнительныеНачисленияБазаОсновныеНачисления.ФизическоеЛицо.ДатаПриема, &Период, МЕСЯЦ) + ДополнительныеНачисленияБазаОсновныеНачисления.ФизическоеЛицо.НачальныйСтаж) / 12 КАК СтажЛет
	               |ПОМЕСТИТЬ ВТ_ДанныеСтажа
	               |ИЗ
	               |	РегистрРасчета.ДополнительныеНачисления.БазаОсновныеНачисления(
	               |			&Измерения,
	               |			&Измерения,
	               |			,
	               |			Регистратор = &Ссылка
	               |				И ВидРасчета.СпособРасчета = ЗНАЧЕНИЕ(Перечисление.СпособыРасчета.ПремияОтСтажа)) КАК ДополнительныеНачисленияБазаОсновныеНачисления
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_ДанныеСтажа.НомерСтроки КАК НомерСтроки,
	               |	ВТ_ДанныеСтажа.РезультатБаза КАК РезультатБаза,
	               |	ПроцентПремииПоСтажу.ПроцентПремии КАК ПроцентПремии
	               |ИЗ
	               |	ВТ_ДанныеСтажа КАК ВТ_ДанныеСтажа
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПроцентПремииПоСтажу КАК ПроцентПремииПоСтажу
	               |		ПО (ВТ_ДанныеСтажа.СтажЛет МЕЖДУ ПроцентПремииПоСтажу.НачалоДиапазона И ВЫБОР
	               |				КОГДА ПроцентПремииПоСтажу.КонецДиапазона = 0
	               |					ТОГДА 9999999999
	               |				ИНАЧЕ ПроцентПремииПоСтажу.КонецДиапазона
	               |			КОНЕЦ)";
	
	НачалоПредыдущегоКваратала = ДобавитьМесяц(НачалоКвартала(Дата), -3);
	
	Измерения = Новый Массив;
	Измерения.Добавить("Подразделение");
	Измерения.Добавить("ФизическоеЛицо");
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Период", Дата);
	Запрос.УстановитьПараметр("Измерения", Измерения);

	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Движение = Движения.ДополнительныеНачисления[Выборка.НомерСтроки - 1];
		Движение.Результат = Выборка.РезультатБаза * Выборка.ПроцентПремии / 100;
		
	КонецЦикла;
	
	Движения.ДополнительныеНачисления.Записать(, Истина);	
	
КонецПроцедуры

